////////////////////////////////////////////////////////////////////////////////
//  РедактированиеОтслеживаемыхРеквизитов
//  
////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ИсходныеДанные = ?(ЗначениеЗаполнено(Параметры.ОтслеживаемыеРеквизиты), ЗначениеИзСтрокиВнутр(Параметры.ОтслеживаемыеРеквизиты), РеквизитФормыВЗначение("ДеревоРеквизитов"));
	
	Дерево = Справочники.инт_ПотокиДанных.ЗаполнитьДеревоРеквизитовПоСсылке(Параметры.СсылкаНаОбъект, ИсходныеДанные);
	
	ЗначениеВРеквизитФормы(Дерево, "ДеревоРеквизитов");

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоРеквизитов
&НаКлиенте
Процедура ДеревоРеквизитовОтслеживатьИзмененияПриИзменении(Элемент)
	ИДТекущейСтроки = Элементы["ДеревоРеквизитов"].ТекущаяСтрока;
	
	Если ИДТекущейСтроки <> Неопределено Тогда
		
		ЭлементКоллекции = ЭтаФорма["ДеревоРеквизитов"].НайтиПоИдентификатору(
		ИДТекущейСтроки);
		
		Если ЭлементКоллекции.ОтслеживатьИзменения = 2 Тогда
			ЭлементКоллекции.ОтслеживатьИзменения = 0;
		КонецЕсли;
		
		УстановкаФлажков(ЭлементКоллекции, ЭлементКоллекции.ОтслеживатьИзменения);
		
		Родитель = ЭлементКоллекции.ПолучитьРодителя();
		Пока Родитель <> Неопределено Цикл
			Родитель.ОтслеживатьИзменения = ?(УстановленноДляВсех(ЭлементКоллекции),
			ЭлементКоллекции.ОтслеживатьИзменения, 2);
			ЭлементКоллекции = Родитель;
			Родитель = ЭлементКоллекции.ПолучитьРодителя();
		КонецЦикла;
		
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура СохранитьИЗакрыть(Команда)
	ОтслеживаемыеРеквизиты = ПолучитьОтслеживаемыеРеквизиты();
	
	Если ОтслеживаемыеРеквизиты = Неопределено Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не выбрано ни одного отслеживаемого поля! Сохранение невозможно!";
		Сообщение.Поле = "ДеревоРеквизитов"; //имя реквизита 
		Сообщение.Сообщить();
        Возврат;
	КонецЕсли;
	Закрыть(ОтслеживаемыеРеквизиты);
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановкаФлажков(ЭлементКоллекции, ЗначениеПометки)
	
	Подчиненные = ЭлементКоллекции.ПолучитьЭлементы();
	Для Каждого ТекЭлемент Из Подчиненные Цикл
		ТекЭлемент.ОтслеживатьИзменения = ЗначениеПометки;
		УстановкаФлажков(ТекЭлемент, ТекЭлемент.ОтслеживатьИзменения);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция УстановленноДляВсех(ЭлементКоллекции)
	
	СоседниеЭлементы =
	ЭлементКоллекции.ПолучитьРодителя().ПолучитьЭлементы();
	Для Каждого ТекЭлемент Из СоседниеЭлементы Цикл
		Если ТекЭлемент.ОтслеживатьИзменения <> ЭлементКоллекции.ОтслеживатьИзменения Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция  ПолучитьОтслеживаемыеРеквизиты()
	ОтслеживаемыеРеквизиты = "";
	Дерево = РеквизитФормыВЗначение("ДеревоРеквизитов");
	Если Дерево.Строки.Найти(1, "ОтслеживатьИзменения", Истина) = Неопределено Тогда
		ОтслеживаемыеРеквизиты = Неопределено;
	Иначе
		ОтслеживаемыеРеквизиты = ЗначениеВСтрокуВнутр(Дерево);
	КонецЕсли;
	Возврат ОтслеживаемыеРеквизиты;
КонецФункции

#КонецОбласти
