#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.КлючЗаписи = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяЗаписьДляФормы = ПолучитьЗаписьРСПоКлючу(Параметры.КлючЗаписи);
	СформированноеСообщениеДЗнач = ФормированиеСообщенияДеревоЗначений(ТекущаяЗаписьДляФормы);

	ЗаписьРСТекущийСтатус = ПолучитьТекущийСтатусПоКлючу(Параметры.КлючЗаписи);

	ЗначениеВРеквизитФормы(ТекущаяЗаписьДляФормы, "Запись");
	ЗначениеВРеквизитФормы(СформированноеСообщениеДЗнач, "СформированноеСообщение");
	ЗначениеВРеквизитФормы(ЗаписьРСТекущийСтатус, "ЗаписьРСТекущийСтатусИсходящих");
	
	СписокОтправка.Параметры.УстановитьЗначениеПараметра("ИдентификаторСообщения",
														 Параметры.ИдентификаторСообщения);
	
КонецПроцедуры
													
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокОтправка

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьСтатус(Команда)
			
	ОткрытьФорму("Перечисление.инт_СтатусыИсходящихСообщений.ФормаВыбора",,,,,,
				Новый ОписаниеОповещения("ИзменитьСтатусЗавершение", ЭтотОбъект));
									
КонецПроцедуры
			
&НаКлиенте
Процедура ИсторияСтатусов(Команда)
		
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ИдентификаторСообщения", Запись.ИдентификаторСообщения);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", ПараметрыОтбора);
	
	ОткрытьФорму("РегистрСведений.инт_ИсторияСтатусовИсходящихСообщений.ФормаСписка", ПараметрыФормы, ЭтотОбъект);
		
КонецПроцедуры
			
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьТекущийСтатусПоКлючу(КлючЗаписи)
		
	Запись = РегистрыСведений.инт_ТекущийСтатусИсходящихСообщений.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(Запись, КлючЗаписи);
	Запись.Прочитать();
	
	Если Запись.Выбран() Тогда
		Возврат Запись;
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьЗаписьРСПоКлючу(КлючЗаписи)
		
	Запись = РегистрыСведений.инт_ОчередьИсходящихСообщений.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(Запись, КлючЗаписи);
	Запись.Прочитать();
	
	Если Запись.Выбран() Тогда
		Возврат Запись;
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ИзменитьСтатусНаСервере(Статус, ИдентификаторСообщения, ТекстОшибки)
		
    РегистрыСведений.инт_ТекущийСтатусИсходящихСообщений.ЗаписатьСтатусСообщения(ИдентификаторСообщения, Статус, ТекстОшибки);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСтатусЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИзменитьСтатусНаСервере(ВыбранныйЭлемент,
							Запись.ИдентификаторСообщения,
							ЗаписьРСТекущийСтатусИсходящих.ТекстОшибки);
										
КонецПроцедуры
						
&НаСервере
Функция ФормированиеСообщенияДеревоЗначений(ТекущаяЗаписьДляФормы)
	
	СформированноеСообщениеСоотв = ТекущаяЗаписьДляФормы.СформированноеСообщение.Получить();
	
	СформированноеСообщениеДЗнач = Новый ДеревоЗначений;
	СформированноеСообщениеДЗнач.Колонки.Добавить("Ключ");
	СформированноеСообщениеДЗнач.Колонки.Добавить("Значение");
	
	СоответствиеВДерево(СформированноеСообщениеСоотв, СформированноеСообщениеДЗнач);
	
	Возврат СформированноеСообщениеДЗнач;
	
КонецФункции

&НаСервере
Процедура СоответствиеВДерево(Соответствие, СтрокаДерева)
	
	Если Тип("Соответствие") = ТипЗнч(Соответствие) Тогда
		ОбработатьСоответствие(Соответствие, СтрокаДерева);
	ИначеЕсли Тип("Массив") = ТипЗнч(Соответствие) Тогда
        ОбработатьМассив(Соответствие, СтрокаДерева);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьСоответствие(Соответствие, СтрокаДерева)
	
	Для Каждого Элемент	Из Соответствие Цикл
		ПодстрокаДерева		= СтрокаДерева.Строки.Добавить();
		ПодстрокаДерева.Ключ	= Элемент.Ключ;
		Если ТипЗнч(Элемент.Значение) = Тип("Строка") Тогда
			ПодстрокаДерева.Значение	= Элемент.Значение;
		Иначе
			СоответствиеВДерево(Элемент.Значение, ПодстрокаДерева)
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьМассив(Соответствие, СтрокаДерева)
	
	Для Инд = 0 По Соответствие.ВГраница() Цикл
		ПодстрокаДерева		= СтрокаДерева.Строки.Добавить();
		ПодстрокаДерева.Ключ	= "ЭлементМассива[" + Инд + "]";
		Если ТипЗнч(Соответствие[Инд]) = Тип("Строка") Тогда
			ПодстрокаДерева.Значение	= Соответствие[Инд];
		Иначе
			СоответствиеВДерево(Соответствие[Инд], ПодстрокаДерева)
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

