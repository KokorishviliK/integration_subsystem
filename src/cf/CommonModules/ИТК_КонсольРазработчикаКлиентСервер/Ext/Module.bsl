#Область ПрограммныйИнтерфейс

// Возвращает представление
//
// Возвращаемое значение:
//   Строка	- Представление
//
Функция Представление() Экспорт
	
	Возврат НСтр("ru = 'Консоль разработчика'; en = 'Developer console'");
	
КонецФункции

Процедура НовыйРезультат(Результат) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Пустой", Истина);
	Результат.Вставить("UID");
	Результат.Вставить("Адрес");
	Результат.Вставить("АдресДанныхРасшифровки");
	Результат.Вставить("АдресСхемыКомпоновкиДанных");
	Результат.Вставить("АдресИсполняемыхЗапросовСКД");
	Результат.Вставить("РежимВыполнения");
	Результат.Вставить("ВсеСтрокиВыведены");
	Результат.Вставить("АктуаленПоСтроке");
	Результат.Вставить("IDПроцесса");
	Результат.Вставить("НачалоВыполнения");
	Результат.Вставить("ОкончаниеВыполненияНаКлиенте");
	Результат.Вставить("ОписанияТиповПараметров");
	
КонецПроцедуры

Процедура ЗаполнениеДанныхСтроки(Строка, Имя, Тип, UID = Неопределено, НачальныеДанные = Неопределено, Картинка = Неопределено) Экспорт
	
	// Заполнение данных строки
	Если ЗначениеЗаполнено(UID) Тогда
		Строка.UID = UID;
	Иначе
		Строка.UID = Новый УникальныйИдентификатор;
	КонецЕсли;
	
	Строка.Имя	= Имя;
	Строка.Тип	= Тип;
	
	Если ИТК_КонсольРазработчикаДанныеКлиентСервер.ЭтоОдинИзТиповСтрокиСодержащийСохраняемыеСтроки(Тип) Тогда
		Строка.Развернуто = Истина;
	КонецЕсли;
	
	Если Тип = ИТК_Перечисления.ЭлементДанныхГруппа() Тогда
		
		Строка.Картинка = БиблиотекаКартинок.ИТК_Папка;
		
	ИначеЕсли Тип = ИТК_Перечисления.ЭлементДанныхЗапрос() Тогда
		
		Строка.Картинка = БиблиотекаКартинок.ИТК_Запрос;
		
	ИначеЕсли Тип = ИТК_Перечисления.ЭлементДанныхСхемаКомпоновкиДанных() Тогда
		
		Строка.Картинка = БиблиотекаКартинок.ИТК_СхемаКомпоновкиДанных;
		
	ИначеЕсли Тип = ИТК_Перечисления.ЭлементДанныхПользовательскаяНастройкаСКД() Тогда
		
		Строка.Картинка = БиблиотекаКартинок.ИТК_ПользовательскаяНастройкаСКД;
		
	ИначеЕсли Тип = ИТК_Перечисления.ЭлементДанныхВариантОтчетаСКД() Тогда
		
		Строка.Картинка = БиблиотекаКартинок.ИТК_ВариантОтчетаСКД;
		
	ИначеЕсли Тип = ИТК_Перечисления.ЭлементДанныхКод() Тогда
		
		Строка.Картинка = БиблиотекаКартинок.ИТК_Код;
		
	ИначеЕсли Тип = ИТК_Перечисления.ЭлементДанныхПодзапрос() Тогда
		
		Строка.Картинка = Картинка;
		
	КонецЕсли;
	
	Если НачальныеДанные = Неопределено Тогда
		НачальныеДанные = ИТК_КонсольРазработчикаДанныеКлиентСервер.НовыйЭлемент(Тип);
	КонецЕсли;
	
	Строка.Данные = НачальныеДанные;
	
КонецПроцедуры

Функция СтрокиДереваДанных(Строка, ОтборТип) Экспорт
	
	Результат = Новый Массив;
	Для Каждого ВложеннаяСтрока Из Строка.ПолучитьЭлементы() Цикл
		
		Если ВложеннаяСтрока.Тип = ОтборТип Тогда
			
			Результат.Добавить(ВложеннаяСтрока);
			
		ИначеЕсли ВложеннаяСтрока.Тип = ИТК_Перечисления.ЭлементДанныхГруппа() Тогда
			
			СтрокиГруппы = СтрокиДереваДанных(ВложеннаяСтрока, ОтборТип);
			Для Каждого СтрокаВГруппе Из СтрокиГруппы Цикл
				
				Результат.Добавить(СтрокаВГруппе);
				
			КонецЦикла;
			
		КонецЕсли;

	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Получает наименование выполняемой операции по режиму
//
// Параметры:
//  Режим  - Перечисление.ИТК_РежимВыполненияЗапроса, Перечисление.ИТК_РежимВыполненияСхемыКомпоновкиДанных - Режим
//
// Возвращаемое значение:
//   Строка - Наименование выполняемой операции
//
Функция НаименованиеВыполняемойОперации(Режим) Экспорт
	
	Результат = Неопределено;
	
	Если Режим = ИТК_Перечисления.РежимВыполненияЗапросаВыполнение() Тогда
		
		Результат = НСтр("ru = 'Выполнение запроса'; en = 'Query execution'");
		
	ИначеЕсли Режим = ИТК_Перечисления.РежимВыполненияЗапросаСРезультатамиВременныхТаблиц() Тогда
		
		Результат = НСтр("ru = 'Выполнение запроса (результат с временными таблицами)'; en = 'Query execution (result with temporary tables)'");
		
	ИначеЕсли Режим = ИТК_Перечисления.РежимВыполненияЗапросаЗамер() Тогда
		
		Результат = НСтр("ru = 'Замер времени выполнения запроса'; en = 'Measurement of request execution time'");
		
	ИначеЕсли Режим = ИТК_Перечисления.РежимВыполненияСКДВыполнение() Тогда
		
		Результат = НСтр("ru = 'Выполнение схемы компоновки данных'; en = 'Execution of the data composition scheme'");
		
	ИначеЕсли Режим = ИТК_Перечисления.РежимВыполненияСКДЗамер() Тогда
		
		Результат = НСтр("ru = 'Замер времени выполнения схемы компоновки данных'; en = 'Measurement of execution time of data composition scheme'");
		
	ИначеЕсли Режим = ИТК_Перечисления.РежимВыполненияСКДПолучениеИсполняемыхЗапросов() Тогда
		
		Результат = НСтр("ru = 'Получение исполняемых запросов СКД'; en = 'Receiving executable queries DCS'");
		
	ИначеЕсли Режим = ИТК_Перечисления.РежимВыполненияКодаВыполнение() Тогда
		
		Результат = НСтр("ru = 'Выполнение кода'; en = 'Execution code'");
		
	ИначеЕсли Режим = ИТК_Перечисления.РежимВыполненияКодаНаКлиенте() Тогда
		
		Результат = НСтр("ru = 'Выполнение кода на клиенте'; en = 'Executing code on the client'");
		
	ИначеЕсли Режим = ИТК_Перечисления.РежимВыполненияКодаЗамер() Тогда
		
		Результат = НСтр("ru = 'Замер времени выполнения кода'; en = 'Measuring code execution time'");
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ЭтоСложныйПараметр(ТипЗначения) Экспорт
	
	Возврат (ТипЗначения = Тип("Структура"));
	
КонецФункции

Функция КартинкаПараметраЗапроса(ТипЗначения) Экспорт
	
	Если ЭтоСложныйПараметр(ТипЗначения)
			И ТипЗначения.Вид = ИТК_Перечисления.СложныйПараметрЗапросаТаблицаЗначений() Тогда
		
		Результат = БиблиотекаКартинок.ВнешнийИсточникДанныхТаблица;
		
	Иначе
		
		Результат = ИТК_ТипыВызовСервера.КартинкаОписания(ТипЗначения);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
