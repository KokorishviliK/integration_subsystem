////////////////////////////////////////////////////////////////////////////////

// инт_ФормированиеИсходящихСообщений

//  

////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс
Процедура ЗапускМенеджераПотоков() Экспорт
    РегистрыСведений.инт_МенеджерПотоковФормированияСообщений.СобратьМусор();
    
    КоличествоПотоковКСозданию = РегистрыСведений.инт_МенеджерПотоковФормированияСообщений.КоличествоПотоковКСозданию();
    Для Итератор=1 По КоличествоПотоковКСозданию Цикл
        СоздатьПотокФормированияСообщений();
    КонецЦикла;
КонецПроцедуры

// Процедура - Выполнить обработку сообщений в потоке
//
// Параметры:
//  МассивСообщений     -   Массив   -  Массив уникальных идентификаторов сообщений которые необходимо обработать в потоке.
//
Процедура ВыполнитьОбработкуСообщенийВПотоке(МассивСообщений)
    Поток = ПолучитьТекущийСеансИнформационнойБазы().ПолучитьФоновоеЗадание();
    Если Поток = Неопределено Тогда
        
    	ВызватьИсключение "Процедура <ВыполнитьОбработкуСообщенийВПотоке> не предназначена для интерактивного выполнения. Она должна работать только в рамках фонового задания.";
    КонецЕсли;
    ЗаписьЖурналаРегистрации("ПодсистемаИнтеграции.МенеджерПотоковФормированияСообщений", УровеньЖурналаРегистрации.Информация,,,СтрШаблон("Запущена обработка сообщений в потоке с ID <%1>", Поток.УникальныйИдентификатор));

    Для Каждого ИдентификаторСообщения Из МассивСообщений Цикл
        Попытка
            РегистрыСведений.инт_ОчередьИсходящихСообщений.СформироватьСообщениеПоИдентификатору(ИдентификаторСообщения);
        Исключение
            СообщениеОбОшибке = СтрШаблон("В потоке с идентификатором <%1> не удалось сформровать сообщение с идентификтором <%2>.
            |
            |Информация об ошибке: %3",
            Поток.УникальныйИдентификатор,
            ИдентификаторСообщения,
            ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
            ЗаписьЖурналаРегистрации("ПодсистемаИнтеграции.ПотокФормированияСообщений", УровеньЖурналаРегистрации.Ошибка,,,СообщениеОбОшибке);
        КонецПопытки;
        
    КонецЦикла;
    
    РегистрыСведений.инт_МенеджерПотоковФормированияСообщений.ОбработатьЗавершениеРаботыПотока(Поток.УникальныйИдентификатор);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Процедура СоздатьПотокФормированияСообщений()
    КоличествоСообщенийВПачке = инт_ФормированиеИсходящихСообщенийПовтИсп.РазмерПачкиФормированияСообщений();
    МассивСообщений = РегистрыСведений.инт_ТекущийСтатусИсходящихСообщений.ПолучитьИдентификаторыСообщенийПоСтатусу(Перечисления.инт_СтатусыИсходящихСообщений.Новый,КоличествоСообщенийВПачке);
    
    Если МассивСообщений.Количество = 0 Тогда
        // Нет сообщений для обработки.
    	Возврат;
    КонецЕсли;
    НачатьТранзакцию();
    Попытка
        РегистрыСведений.инт_ТекущийСтатусИсходящихСообщений.ЗаписатьСтатусСообщений(МассивСообщений, Перечисления.инт_СтатусыИсходящихСообщений.ФормированиеСообщения);
        МассивПараметров = Новый Массив;
        МассивПараметров.Добавить(МассивСообщений);
        Поток = ФоновыеЗадания.Выполнить("инт_ФормированиеИсходящихСообщений.ВыполнитьОбработкуСообщенийВПотоке", МассивПараметров, ,"Поток формирования исходящих сообщений.");
        РегистрыСведений.инт_МенеджерПотоковФормированияСообщений.ЗарегистрироватьПоток(Поток.УникальныйИдентификатор, МассивСообщений);
        ЗафиксироватьТранзакцию();
    Исключение
        ОтменитьТранзакцию();
        СообщениеОбОшибке = СтрШаблон("Ошибка при создании потока формирования сообщений.
        |
        |Информация об ошибке: %1", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
        ЗаписьЖурналаРегистрации("ПодсистемаИнтеграции.МенеджерПотоковФормированияСообщений", УровеньЖурналаРегистрации.Ошибка,,,СообщениеОбОшибке);
    КонецПопытки;
    
КонецПроцедуры

#КонецОбласти
