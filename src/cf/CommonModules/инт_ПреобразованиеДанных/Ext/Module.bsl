#Область ПрограммныйИнтерфейс

Процедура ЗначениеВДерево(Ключ, Значение, КоллекцияСтрок) Экспорт
	
	Строка = КоллекцияСтрок.Добавить();
	Строка.Ключ = Ключ;
	Строка.ТипЗначения = ТипЗнч(Значение);
	
	Если ЭтоОбъект(Значение) Тогда
		ОбъектВДерево(Значение, Строка.Строки);
	ИначеЕсли ЭтоМассив(Значение) Тогда
		МассивВДерево(Значение, Строка.Строки)
	Иначе // Это примитив
		Строка.Значение = Значение;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбъектВДерево(Объект, КоллекцияСтрок)
	
	Для Каждого ЭлементОбъекта Из Объект Цикл
		ЗначениеВДерево(ЭлементОбъекта.Ключ, ЭлементОбъекта.Значение, КоллекцияСтрок);
	КонецЦикла;
	
КонецПроцедуры

Процедура МассивВДерево(Массив, КоллекцияСтрок)
	
	СчетчикСтрок = 0;
	Для Каждого ЭлементМассива Из Массив Цикл
		ЗначениеВДерево(СтрШаблон("СтрокаМассива[%1]", СчетчикСтрок), ЭлементМассива, КоллекцияСтрок);
		СчетчикСтрок = СчетчикСтрок+1;
	КонецЦикла;
	
КонецПроцедуры

Функция ЭтоОбъект(Значение)
	Возврат ТипЗнч(Значение) = Тип("Соответствие") ИЛИ ТипЗнч(Значение) = Тип("Структура");
КонецФункции

Функция ЭтоМассив(Значение)
	Возврат ТипЗнч(Значение) = Тип("Массив");
КонецФункции

#КонецОбласти
