#Область ПрограммныйИнтерфейс

Процедура ОткрытьИнструмент(Инструмент, Параметр = Неопределено) Экспорт
	
	Если Инструмент = ИТК_Перечисления.ИнструментОткрытьОбъект() Тогда
		
		ПоказатьЗначение( , Параметр);
		Возврат;
		
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	Если Инструмент = ИТК_Перечисления.ИнструментКонсольЗапросов() Тогда
		
		ПолноеИмяФормы = "Обработка.ИТК_КонсольРазработчика.Форма";
		ПараметрыФормы.Вставить("Команда", "КонсольЗапросов");
		
	ИначеЕсли Инструмент = ИТК_Перечисления.ИнструментКонсольСхемКомпоновкиДанных() Тогда
		
		ПараметрыФормы.Вставить("Команда", "КонсольСКД");
		ПолноеИмяФормы = "Обработка.ИТК_КонсольРазработчика.Форма";
		
	ИначеЕсли Инструмент = ИТК_Перечисления.ИнструментКонсольКода() Тогда
		
		ПараметрыФормы.Вставить("Команда", "КонсольКода");
		ПолноеИмяФормы = "Обработка.ИТК_КонсольРазработчика.Форма";
		
	ИначеЕсли Инструмент = ИТК_Перечисления.ИнструментПодпискиНаСобытия() Тогда
		
		ПолноеИмяФормы = "Обработка.ИТК_ПодпискиНаСобытия.Форма";
		
	ИначеЕсли Инструмент = ИТК_Перечисления.ИнструментАнализПравДоступа() Тогда
		
		ПолноеИмяФормы = "Отчет.ИТК_АнализПравДоступа.Форма";
		
	ИначеЕсли Инструмент = ИТК_Перечисления.ИнструментСравнениеОбъектов() Тогда
		
		ПолноеИмяФормы = "Отчет.ИТК_СравнениеОбъектов.Форма";
		
	ИначеЕсли Инструмент = ИТК_Перечисления.ИнструментПоискИЗаменаСсылок() Тогда
		
		ПолноеИмяФормы = "Обработка.ИТК_ПоискИЗаменаСсылок.Форма";
		
	ИначеЕсли Инструмент = ИТК_Перечисления.ИнструментПоискСсылокНаОбъект() Тогда
		
		ПолноеИмяФормы = "Обработка.ИТК_ПоискСсылокНаОбъект.Форма";
		
	ИначеЕсли Инструмент = ИТК_Перечисления.ИнструментПользователи() Тогда
		
		ПолноеИмяФормы = "Обработка.ИТК_Пользователи.Форма";
		
	ИначеЕсли Инструмент = ИТК_Перечисления.ИнструментРегламентныеИФоновыеЗадания() Тогда
		
		ПолноеИмяФормы = "Обработка.ИТК_РегламентныеИФоновыеЗадания.Форма";
		
	ИначеЕсли Инструмент = ИТК_Перечисления.ИнструментРедакторКонстант() Тогда
		
		ПолноеИмяФормы = "Обработка.ИТК_РедакторКонстант.Форма";
		
	ИначеЕсли Инструмент = ИТК_Перечисления.ИнструментРедакторОбъекта() Тогда
		
		ПолноеИмяФормы = "Обработка.ИТК_РедакторОбъекта.Форма";
		
	ИначеЕсли Инструмент = ИТК_Перечисления.ИнструментРедакторПараметровСеанса() Тогда
		
		ПолноеИмяФормы = "Обработка.ИТК_РедакторПараметровСеанса.Форма";
		
	ИначеЕсли Инструмент = ИТК_Перечисления.ИнструментМониторЛицензий() Тогда
		
		ПолноеИмяФормы = "Отчет.ИТК_МониторЛицензий.Форма";
		
	ИначеЕсли Инструмент = ИТК_Перечисления.ИнструментЖурналРегистрации() Тогда
		
		ПолноеИмяФормы = ИТК_БСПВызовСервера.ПолноеИмяФормыСтандартнойОбработки("ЖурналРегистрации");
		
	КонецЕсли;
	
	Уникальность = Истина;
	ЗаполнитьПараметрыЗапускаИнструмента(Инструмент, ПараметрыФормы, Параметр, Уникальность);

	ОткрытьФорму(ПолноеИмяФормы, ПараметрыФормы, , Уникальность, , , , РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

Функция ПоказатьПредупреждениеНедоступноВОграниченнойВерсии() Экспорт
	
	Если ИТК_ТуллкитКлиентСервер.ОграниченнаяВерсия() Тогда
		
		Результат = Истина;
		
		ТекстПредупреждения = НСтр("ru = 'Данная функция недоступна в вашей версии (ADMIN)'; en = 'This function is not available in your version (ADMIN)'");
		ПоказатьПредупреждение( , ТекстПредупреждения, , ИТК_ТуллкитКлиентСервер.Представление());
		
	Иначе
		
		Результат = Ложь;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПараметрыЗапускаИнструмента(Инструмент, ПараметрыФормы, Параметр, Уникальность)
	
	Если Параметр = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Инструмент = ИТК_Перечисления.ИнструментПоискИЗаменаСсылок() Тогда
		
		ПараметрыФормы.Вставить("Ссылки", СписокСсылокИнструмента(Параметр));
		
	ИначеЕсли Инструмент = ИТК_Перечисления.ИнструментСравнениеОбъектов() Тогда
		
		СформироватьПриОткрытии = ПараметрСписокСсылокИнструмента(Параметр)
										И Параметр.Количество() > 1;
		ПараметрыФормы.Вставить("СформироватьПриОткрытии", СформироватьПриОткрытии);
		
		ПараметрыФормы.Вставить("Ссылки", СписокСсылокИнструмента(Параметр));
		
	ИначеЕсли Инструмент = ИТК_Перечисления.ИнструментПоискСсылокНаОбъект()
				ИЛИ Инструмент = ИТК_Перечисления.ИнструментРедакторОбъекта()Тогда
		
		ПараметрыФормы.Вставить("Объект", Параметр);
		Уникальность = Параметр.УникальныйИдентификатор();
		
	ИначеЕсли Инструмент = ИТК_Перечисления.ИнструментЖурналРегистрации() Тогда
		
		ПараметрыФормы.Вставить("Данные", Параметр);
		Уникальность = Параметр.УникальныйИдентификатор();
		
	ИначеЕсли Инструмент = ИТК_Перечисления.ИнструментАнализПравДоступа() Тогда
		
		СформироватьПриОткрытии = (Параметр <> Неопределено);
		ПараметрыФормы.Вставить("СформироватьПриОткрытии", СформироватьПриОткрытии);
		
		ОтборМетаданных = ИТК_ФормаВызовСервера.ОтборПоТипамСсылок(Параметр);
		ПараметрыФормы.Вставить("ОтборМетаданных", ОтборМетаданных);
		
	ИначеЕсли Инструмент = ИТК_Перечисления.ИнструментПодпискиНаСобытия() Тогда
		
		ОтборМетаданных = ИТК_ФормаВызовСервера.ОтборПоТипамСсылок(Параметр);
		ПараметрыФормы.Вставить("ОтборМетаданных", ОтборМетаданных);
		
	КонецЕсли;
	
КонецПроцедуры

Функция СписокСсылокИнструмента(Параметр)
	
	Если ТипЗнч(Параметр) = Тип("СписокЗначений") Тогда
		
		Результат = Параметр.ВыгрузитьЗначения();
		
	ИначеЕсли ТипЗнч(Параметр) = Тип("Массив") Тогда
		
		Результат = Параметр;
		
	Иначе
		
		Результат = Новый Массив;
		Результат.Добавить(Параметр);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПараметрСписокСсылокИнструмента(Параметр)
	
	Результат = ТипЗнч(Параметр) = Тип("СписокЗначений")
					ИЛИ ТипЗнч(Параметр) = Тип("Массив");
					
	Возврат Результат;
	
КонецФункции

#КонецОбласти
